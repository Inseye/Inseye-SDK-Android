plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.inseye.sdk'
    compileSdk 34

    defaultConfig {
        minSdk 27
        versionCode 1
        versionName '0.1-alpha'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    libraryVariants.configureEach { variant ->
        variant.outputs.all {
            outputFileName = "inseye_sdk_android_${variant.name}-${defaultConfig.versionName}.aar"
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.inseye'
            artifactId = 'inseye-sdk'
            version = '0.1-alpha'

            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            url = uri("file://${rootProject.buildDir}/repo")
        }
    }
}

dependencies {
    implementation project(':inseye_sdk:inseye_client_api')
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}