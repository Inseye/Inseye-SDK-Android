plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def libraryVersion = '0.3.0'

android {
    namespace 'com.inseye.sdk'
    compileSdk 35


    defaultConfig {
        buildFeatures.buildConfig true
        minSdk 27
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        buildConfigField "String", "INSEYE_SDK_VERSION", "\"${libraryVersion}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    libraryVariants.configureEach { variant ->
        variant.outputs.all {
            outputFileName = "inseye_sdk_android_${variant.name}-${defaultConfig.versionName}.aar"
        }
    }
}

dependencies {
    api project(':inseye_client_sdk:inseye_client_api')

    implementation libs.androidx.appcompat
    implementation libs.material
    api libs.commons.math3


    compileOnly libs.lombok
    annotationProcessor libs.lombok

    testImplementation libs.junit
    implementation libs.androidx.rules
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}


publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.inseye'
            artifactId = 'inseye-sdk'
            version = libraryVersion

            afterEvaluate {
                from components.release
            }
        }
    }
}